plugins {
    id 'java'
    id 'application'
    id 'com.github.johnrengelman.shadow' version '5.1.0'
}

description = 'Master Password GUI'
mainClassName = 'com.lyndir.masterpassword.gui.MasterPassword'

dependencies {
    implementation group: 'com.lyndir.lhunath.opal', name: 'opal-system', version: '1.7-p2'
    implementation group: 'ch.qos.logback', name: 'logback-classic', version: '1.1.2'
    implementation group: 'com.yuvimasory', name: 'orange-extensions', version: '1.3.0'
    implementation group: 'com.github.tulskiy', name: 'jkeymaster', version: '1.2'
    implementation group: 'com.github.spotbugs', name: 'spotbugs-annotations', version: '4.2.1'

    compile project( ':masterpassword-model' )
}


// release with: STORE_PW=$(mpw masterpassword.keystore) KEY_PW_DESKTOP=$(mpw masterpassword-desktop) gradle clean masterpassword-gui:shadowJar
shadowJar {
    manifest {
        attributes 'Implementation-Title': description
        attributes 'Implementation-Version': version
    }
    doLast {
        if (System.getenv( 'KEY_PW_DESKTOP' ) != null)
            ant.signjar( jar: archivePath,
                         alias: 'masterpassword-desktop',
                         keystore: 'masterpassword.keystore',
                         storepass: System.getenv( 'STORE_PW' ),
                         keypass: System.getenv( 'KEY_PW_DESKTOP' ),
                         preservelastmodified: 'true',
                         signedJar: "${rootDir}/public/site/${project.name}-${project.version}.jar" )
    }
}

run {
    // I don't fully understand why this is necessary, but without it -Dmp.log.level is lost.
    //systemProperties = System.properties
}
